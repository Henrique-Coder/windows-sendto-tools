@echo off
chcp 65001 >nul 2>&1
setlocal EnableDelayedExpansion

rem Set terminal title
title Fileditch Sender - Developed at gh@Henrique-Coder/windows-sendto-tools

rem Check if a file is provided as an argument
if "%~1"=="" (
    echo Error: No file specified.
    goto endapp
)

rem Check if the file has an extension and disallow certain extensions
set "valid_extensions=exe bat cmd vbs 7z"
set "file_extension=%~x1"
if "%file_extension%"=="" (
    echo Error: File has no extension. Upload aborted.
    goto endapp
)

rem Validate against disallowed extensions
for %%E in (%valid_extensions%) do (
    if /i "%file_extension:~1%"=="%%E" (
        echo Error: Uploading files with extension ".%%E" is not allowed.
        goto endapp
    )
)

rem Ask the user if they want to upload the file permanently
echo. && set /p "permanent_upload=Do you want to upload the file permanently? (0 = No (default), 1 = Yes) "
if not defined permanent_upload set "permanent_upload=0"

rem Setting up the upload type and the corresponding strings
if !permanent_upload! equ 0 (
    set "upload_type=/temp/" && set "upload_long_string=Temporary FileDitch URL (expires in 72h)" && set "upload_word=temporary"
) else (
    set "upload_type=/" && set "upload_long_string=Permanent FileDitch URL (never expires)" && set "upload_word=permanent"
)

rem Ensure that 'curl' is installed and available in the PATH
where curl >nul 2>&1
if errorlevel 1 (
    echo Error: 'curl.exe' binary not found. Please install 'curl' and make sure it's in the PATH.
    goto endapp
)

rem Clearing the screen and showing program information
cls
echo.
echo + -- - Developer: https://github.com/Henrique-Coder - -- +
echo.
echo -- --- - Uploading Status (!upload_long_string!) - --- --
echo.

rem Set the initial value for the filename
set "filename=%temp%\_temp_fileditch_response.json"

rem Remove the temporary file, if it exists
del /f /q "%filename%" >nul 2>&1

rem Calculating the file size in bytes, converting it to kilobytes and then to megabytes
for %%F in ("%~1") do set "filesize_b=%%~zF"
set /a filesize_kb=!filesize_b! / 1024
set /a filesize_mb=!filesize_kb! / 1024

rem Uploading the file, getting the direct link, and copying it to the clipboard
curl -i -F "files[]=@%~1" https://up1.fileditch.com%upload_type%upload.php | findstr "\"url\"" > "%filename%"
if errorlevel 1 (
    echo Error: Failed to upload the file. Please check your internet connection and try again later.
    goto endapp
)

rem Extracting the direct link from the response
set /p output_url=< "%filename%"
set output_url=%output_url:"url":=%
set output_url=%output_url:"=%
set output_url=%output_url:,=%
set output_url=%output_url: =%
set output_url=%output_url:\/=/%

rem Copying the direct link to the clipboard and removing the temporary file
echo !output_url! | clip >nul 2>&1
del /f /q "%filename%" >nul 2>&1

echo.
echo -- --- - The direct !upload_word! url was copied to the clipboard - --- --
echo.
echo + Filepath: %~1
echo + URL: !output_url!
echo.

rem Check whether the log file exists and is empty, and if it is, it will be created with the correct formatting
set "log_file=%~dp0upload_logs.txt"
set "default_logfile_string=Logs generated by the "fileditch-sender" script at "https://github.com/Henrique-Coder/windows-sendto-tools""

if not exist "%log_file%" (
    echo %default_logfile_string% > "%log_file%"
    goto :_continue
)

for %%I in ("%log_file%") do (
    if %%~zI equ 0 (
        echo %default_logfile_string% > "%log_file%"
    )
)

:_continue

rem Creating a temporary file to hold the new log
set "temp_log_file=%temp%\.temp_fileditch_urls.txt"
echo Upload log from %date:/=-% at %time:~0,8% → >> "%temp_log_file%"
echo    Filepath: "%~1" >> "%temp_log_file%"
echo    Size: %filesize_b% B (exactly) ㅣ %filesize_kb% KB (approx.) ㅣ %filesize_mb% MB (approx.) >> "%temp_log_file%"
echo    URL: !output_url! >> "%temp_log_file%"
echo. >> "%temp_log_file%"

rem Appending the previous logs to the temporary file, if it exists
if exist "%log_file%" (
    type "%log_file%" >> "%temp_log_file%"
)

rem Replacing the original file with the temporary file
move /Y "%temp_log_file%" "%log_file%" >nul

goto endapp

:endapp
endlocal
pause >nul 2>&1
exit /b %errorlevel%
